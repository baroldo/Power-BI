DynamicCumulativeMarginPercent_ByCategory = 
VAR CurrentPercentile = MAX('PercentileAxis'[Percentage of Merchants (%)])
VAR SelectedCategory = SELECTEDVALUE('GroupBySelector'[GroupBySelector Fields], "All")

VAR BaseTable =
    ADDCOLUMNS(
        FILTER(
            ALLSELECTED('MARGIN_DECILIES'),
            NOT(ISBLANK('MARGIN_DECILIES'[MARGIN_$]))
        ),
        "GroupValue",
        SWITCH(
            SelectedCategory,
            "'MARGIN_DECILIES'[SECTOR_DESC]", 'MARGIN_DECILIES'[SECTOR_DESC],
            "'MARGIN_DECILIES'[PROD_CAT]", 'MARGIN_DECILIES'[PROD_CAT],
            "'MARGIN_DECILIES'[INDUSTRY]", 'MARGIN_DECILIES'[INDUSTRY],
            "All", "All"
        )
    )

VAR GroupValues =
    DISTINCT(SELECTCOLUMNS(BaseTable, "GroupValue", [GroupValue]))

VAR ResultTable =
    ADDCOLUMNS(
        GroupValues,
        "CumulativeMargin",
            VAR CurrentGroup = [GroupValue]
            VAR FilteredGroup =
                FILTER(BaseTable, [GroupValue] = CurrentGroup)
            VAR RankedGroup =
                ADDCOLUMNS(
                    FilteredGroup,
                    "Rank",
                        RANKX(
                            FilteredGroup,
                            'MARGIN_DECILIES'[CustomerPercentileRankIncremental],
                            ,
                            DESC,
                            DENSE
                        )
                )
            VAR CountGroup = COUNTROWS(FilteredGroup)
            VAR ThresholdRank = ROUNDUP(CurrentPercentile / 100 * CountGroup, 0)
            VAR CumMargin =
                SUMX(
                    FILTER(RankedGroup, [Rank] <= ThresholdRank),
                    'MARGIN_DECILIES'[MARGIN_$]
                )
            VAR TotalMargin = SUMX(FilteredGroup, 'MARGIN_DECILIES'[MARGIN_$])
            RETURN
                DIVIDE(CumMargin, TotalMargin)
    )

RETURN
    MAXX(
        FILTER(ResultTable,
            [GroupValue] = 
                SWITCH(
                    SelectedCategory,
                    "'MARGIN_DECILIES'[SECTOR_DESC]", SELECTEDVALUE('MARGIN_DECILIES'[SECTOR_DESC]),
                    "'MARGIN_DECILIES'[PROD_CAT]", SELECTEDVALUE('MARGIN_DECILIES'[PROD_CAT]),
                    "'MARGIN_DECILIES'[INDUSTRY]", SELECTEDVALUE('MARGIN_DECILIES'[INDUSTRY]),
                    "All", "All"
                )
        ),
        [CumulativeMargin]
    )
